;; W6 pure-request oracle: cloudflare.analytics GraphQL query text
;; vs kotoba/analytics_core.kotoba.

(ns cloudflare.analytics-kotoba-parity-test
  (:require [clojure.string :as str]
            [clojure.test :refer [deftest is testing]]
            [cloudflare.analytics :as analytics]
            [kotoba.compiler.core :as compiler]
            [kotoba.kir :as ir]))

(def port-source (slurp "kotoba/analytics_core.kotoba"))

(def export-prefix
  "daily-query path-query path-query-declares-host? path-query-filters-host?")

(defn- compile-string-cases [cases]
  (let [defs (for [[name body] cases]
               (str "(defn " name " [] :string " body ")"))
        names (map first cases)
        src (-> port-source
                (str/replace-first
                 #"\(:export \[[^\]]+\]\)"
                 (str "(:export [" export-prefix " " (str/join " " names) "])"))
                (str "\n" (str/join "\n" defs)))
        kir (:kir (compiler/compile-source src :wasm32-kotoba-v1 {}))]
    (into {} (map (fn [n] [n (ir/execute kir (symbol n) [])]) names))))

(defn- compile-i64-cases [cases]
  (let [defs (for [[name body] cases]
               (str "(defn " name " [] :i64 " body ")"))
        names (map first cases)
        src (-> port-source
                (str/replace-first
                 #"\(:export \[[^\]]+\]\)"
                 (str "(:export [" export-prefix " " (str/join " " names) "])"))
                (str "\n" (str/join "\n" defs)))
        kir (:kir (compiler/compile-source src :wasm32-kotoba-v1 {}))]
    (into {} (map (fn [n] [n (ir/execute kir (symbol n) [])]) names))))

(deftest daily-query-matches-analytics
  (let [actual (compile-string-cases {"dq" "(daily-query)"})]
    (is (= analytics/daily-query (get actual "dq")))
    (is (= (:query (analytics/daily-report-request
                    {:zone-tag "z" :since "a" :until "b"}))
           (get actual "dq")))))

(deftest path-query-matches-analytics
  (let [actual (compile-string-cases
                {"p0" "(path-query 0)"
                 "p1" "(path-query 1)"})]
    (is (= (analytics/path-query false) (get actual "p0")))
    (is (= (analytics/path-query true) (get actual "p1")))
    (is (= (:query (analytics/path-report-request
                    {:zone-tag "z1" :since "s" :until "u"}))
           (get actual "p0")))
    (is (= (:query (analytics/path-report-request
                    {:zone-tag "z1" :since "s" :until "u" :host "example.com"}))
           (get actual "p1")))))

(deftest path-query-host-flags-match-includes-checks
  (let [actual (compile-i64-cases
                {"d0" "(path-query-declares-host? 0)"
                 "d1" "(path-query-declares-host? 1)"
                 "f0" "(path-query-filters-host? 0)"
                 "f1" "(path-query-filters-host? 1)"})]
    (is (= 0 (get actual "d0")))
    (is (= 1 (get actual "d1")))
    (is (= 0 (get actual "f0")))
    (is (= 1 (get actual "f1")))
    (testing "align with existing unit tests on path-query text"
      (is (not (str/includes? (analytics/path-query false) "$host")))
      (is (str/includes? (analytics/path-query true) "$host: String!")))))
