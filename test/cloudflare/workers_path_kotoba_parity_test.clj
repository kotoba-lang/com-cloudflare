;; W6 pure-request oracle: workers/zones/pages REST path construction
;; vs kotoba/workers_path_core.kotoba.

(ns cloudflare.workers-path-kotoba-parity-test
  (:require [clojure.string :as str]
            [clojure.test :refer [deftest is testing]]
            [cloudflare.client :as client]
            [kotoba.compiler.core :as compiler]
            [kotoba.kir :as ir]))

(def port-source (slurp "kotoba/workers_path_core.kotoba"))

(def export-prefix
  "zone-routes-path custom-domains-path scripts-path list-zones-path dns-records-path pages-projects-path")

(defn- kotoba-literal [s]
  (str \" (-> s (str/replace "\\" "\\\\") (str/replace "\"" "\\\"")) \"))

(defn- compile-string-cases [cases]
  (let [defs (for [[name body] cases]
               (str "(defn " name " [] :string " body ")"))
        names (map first cases)
        src (-> port-source
                (str/replace-first
                 #"\(:export \[[^\]]+\]\)"
                 (str "(:export [" export-prefix " " (str/join " " names) "])"))
                (str "\n" (str/join "\n" defs)))
        kir (:kir (compiler/compile-source src :wasm32-kotoba-v1 {}))]
    (into {} (map (fn [n] [n (ir/execute kir (symbol n) [])]) names))))

(deftest rest-paths-match-product-surfaces
  ;; Paths mirror the string args workers/zones/pages pass to client/rest!.
  (let [zone "zone99"
        acct "acct1"
        actual (compile-string-cases
                {"zr" (str "(zone-routes-path " (kotoba-literal zone) ")")
                 "cd" (str "(custom-domains-path " (kotoba-literal acct) ")")
                 "sc" (str "(scripts-path " (kotoba-literal acct) ")")
                 "lz" "(list-zones-path)"
                 "dns" (str "(dns-records-path " (kotoba-literal zone) ")")
                 "pp" (str "(pages-projects-path " (kotoba-literal acct) ")")})]
    (is (= (str "/zones/" zone "/workers/routes") (get actual "zr")))
    (is (= (str "/accounts/" acct "/workers/domains") (get actual "cd")))
    (is (= (str "/accounts/" acct "/workers/scripts") (get actual "sc")))
    (is (= "/zones" (get actual "lz")))
    (is (= (str "/zones/" zone "/dns_records") (get actual "dns")))
    (is (= (str "/accounts/" acct "/pages/projects") (get actual "pp")))
    (testing "url join with api-base matches client convention"
      (is (= (str client/api-base (get actual "zr"))
             (str "https://api.cloudflare.com/client/v4/zones/" zone "/workers/routes"))))))
